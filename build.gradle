plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
}

group 'org.padler'
version '1.1.0'

sourceCompatibility = 1.8

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/org.assertj/assertj-core
    testImplementation 'org.assertj:assertj-core:3.20.2'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
}

sonarqube {
    properties {
        property("sonar.projectName", "Natorder")
        property("sonar.projectKey", "616slayer616_natorder")
        property("sonar.organization", "616slayer616-github")
        property("sonar.host.url", "https://sonarcloud.io")
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'natorder'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Natorder'
                description = 'Java Natural Order comparator'
                url = 'http://github.com/616slayer616/natorder'
                licenses {
                    license {
                        name = 'License'
                        url = 'https://github.com/616slayer616/natorder/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = '616slayer616'
                        name = 'Patrick Adler'
                        email = 'patrick@padler.org'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/616slayer616/natorder.git'
                    developerConnection = 'scm:git:ssh://github.com/616slayer616/natorder.git'
                    url = 'http://github.com/616slayer616/natorder'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/616slayer616/natorder"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
